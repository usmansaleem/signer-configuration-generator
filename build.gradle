// See gradle/libs.versions.toml for the versions of the dependencies.
plugins {
    id 'application'
    alias(libs.plugins.spotless)
    alias(libs.plugins.jgitver)
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
    maven {
        url "https://artifacts.consensys.net/public/teku/maven/"
    }
    maven {
        url "https://artifacts.consensys.net/public/maven/maven/"
    }

}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation libs.picocli
    annotationProcessor libs.picocli.codegen

    implementation libs.slf4j.api
    runtimeOnly libs.slf4j.simple
    runtimeOnly libs.log4j.slf4j.adapter

    implementation libs.bundles.teku
    implementation libs.jblst

    implementation libs.tuweni.bytes
    implementation libs.snakeyaml

    // Use JUnit Jupiter API for testing.
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

tasks.named('test') {
    // Use junit platform for unit tests.
    useJUnitPlatform()
}

application {
    // Define the main class for the application.
    mainClass = 'web3signer.configuration.generator.App'
}

jar {
    doFirst {
        manifest {
            attributes 'X-GIT-SHA1' : "$project.ext.git_sha1_full"
            attributes 'Implementation-Version': "$project.ext.calculated_version"
        }
    }
}

spotless {
    java {
        removeUnusedImports()
        googleJavaFormat()
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeaderFile "${rootDir}/gradle/spotless.java.license"
    }
}

jgitver { nonQualifierBranches "main" }

